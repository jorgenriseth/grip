rule create_lutfile:
  output:
    "data/mri_processed_data/freesurfer_lut.json"
  shell:
    "python scripts/create_freesurfer_lut.py"


rule recon_all_setup:
  input:
    t1="data/mri_dataset/{subject}/ses-01/anat/{subject}_ses-01_T1w.nii.gz",
    FLAIR="data/mri_dataset/{subject}/ses-01/anat/{subject}_ses-01_FLAIR.nii.gz"
  output:
    t1="data/mri_processed_data/freesurfer/{subject}/mri/orig/001.mgz",
    FLAIR="data/mri_processed_data/freesurfer/{subject}/mri/orig/FLAIRraw.mgz"
  shell:
    "mri_convert {input.t1} {output.t1} && "
    "mri_convert --no_scale 1 {input.FLAIR} {output.FLAIR}"


rule recon_all_FLAIR:
  input:
    t1="data/mri_processed_data/freesurfer/{subject}/mri/orig/001.mgz",
    FLAIR="data/mri_processed_data/freesurfer/{subject}/mri/orig/FLAIRraw.mgz"
  output:
    segmentations=protected(expand(
      "data/mri_processed_data/freesurfer/{{subject}}/mri/{seg}.mgz",
      seg=["aparc+aseg", "aseg", "wmparc"]
    )),
    surfs=protected(expand(
      "data/mri_processed_data/freesurfer/{{subject}}/surf/{surf}",
      surf=["lh.pial", "rh.pial", "lh.white", "rh.white"]
    ))
  resources:
    time="48:00:00",
  shell:
    "recon-all"
    " -sd $(realpath $(dirname {output.segmentations[0]})/../../)"
    " -s {wildcards.subject}"
    " -FLAIR {input.FLAIR}"
    " -FLAIRpial"
    " -all"
